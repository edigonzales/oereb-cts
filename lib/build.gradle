plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

apply from: "$rootDir/gradle/versioning.gradle"

group = 'io.github.sogis'
def myArtifactId = "oereb-cts"

sourceCompatibility = '17'
targetCompatibility = '17'

compileJava {
    options.compilerArgs.addAll(['--release', '17'])
}

repositories {
    mavenCentral()
    maven { url "https://jars.interlis.ch" }
    //maven { url "https://artifactory.openpreservation.org/artifactory/vera-dev/" }
}

// "The package javax.xml.xxx is accessible from more than one module: <unnamed>, java.xml"
configurations {
    all*.exclude group: 'xml-apis'
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.10.0'
    
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'ch.interlis:ili2c-core:5.3.3'
    implementation 'ch.interlis:ili2c-tool:5.3.3'
    implementation 'ch.interlis:iox-ili:1.21.18'
    implementation 'net.sf.saxon:Saxon-HE:11.4' 
    implementation 'org.verapdf:validation-model:1.22.2'
    //implementation 'org.verapdf:pdfbox-validation-model:1.22.2'   
}

tasks.named('test') {
    useJUnitPlatform {
        excludeTags 'external'
    }
}

tasks.register('externalTest', Test) {
    description = 'Runs tests with external dependencies'
    group = 'verification'
    useJUnitPlatform {
        includeTags 'external'
    }
}


project.archivesBaseName = myArtifactId

task sourcesJar(type: Jar, dependsOn: classes) {
    baseName myArtifactId
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    baseName myArtifactId
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId = group
            artifactId = myArtifactId

            pom {
                name = "oereb-cts"
                url = "https://github.com/edigonzales/oereb-cts"
                description = myArtifactId
                scm {
                    connection = "scm:git:https@github.com/edigonzales/oereb-cts"
                    developerConnection = "scm:git:https@github.com/edigonzales/oereb-cts"
                    url = "https://github.com/edigonzales/oereb-cts"
                }
                licenses {
                    license {
                        name = "MIT"
                        url = "https://github.com/edigonzales/oereb-cts/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "edigonzales"
                        name = "Stefan Ziegler"
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
